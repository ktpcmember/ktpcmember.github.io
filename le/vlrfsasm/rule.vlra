
{.; ; (c.convertSbS8 (' "<!DOCTYPE html><html>" (hh.content) (hb.content) "</html>"))}

[Html File Path]
{hp.depth; ; (hp.depth.loop (hp.path) (' 0 (c.getLengthSx (hp.path))))}
{hp.depth.loop Path 16 Count; ;
  (?
    (== (: Count 64) 1)
    (?
      (== Path "/")
      (+ (> Count 64) 1)
      (> Count 64)
    )
    (c.empty)
  );
  ('
    (+ (> Count 64) (== Path "/"))
    (- (: Count 64) 1)
  )
}
{hp.rootPath; ; (c.repeat "../" (hp.depth))}
{hp.getRelativePath AbsolutePath; ; (' (hp.rootPath) AbsolutePath)}

[Html header]
{hh.content; ;
  (hb.tagE "head"
    ('
      (hb.tagA "meta" (hb.attribute "charset" "UTF-8"))
      (hb.tagA "meta"
        ('
          (hb.attribute "name" "keywords")
          (hb.attribute "content" (' "駒場東邦物理部,駒場東邦,駒東,KTPC,物理部," (hh.keyword)))
        )
      )
      (hb.tagA "meta"
        ('
          (hb.attribute "name" "description")
          (hb.attribute "content" (hh.description))
        )
      )
      (hb.tagA "meta"
        ('
          (hb.attribute "name" "author")
          (hb.attribute "content" "駒場東邦物理部")
        )
      )
      (hb.tagA "meta"
        ('
          (hb.attribute "http-equiv" "content-language")
          (hb.attribute "content" "ja")
        )
      )
      (hb.tagA "meta"
        ('
          (hb.attribute "name" "viewport")
          (hb.attribute "content" "width=device-width, initial-scale=1, maximum-scale=1")
        )
      )
      (hb.tagE "title" (hh.title))
      (hb.tagA "link"
        ('
          (hb.attribute "rel" "icon")
          (hb.attribute "type" "image/jpeg")
          (hb.attribute "href" (hp.getRelativePath "images/logo.jpg"))
        )
      )
      (hb.tagA "link"
        ('
          (hb.attribute "rel" "stylesheet")
          (hb.attribute "type" "text/css")
          (hb.attribute "href" "default.css")
        )
      )
      (hb.tagA "link"
        ('
          (hb.attribute "rel" "stylesheet")
          (hb.attribute "type" "text/css")
          (hb.attribute "media" "(max-width:800px)")
          (hb.attribute "href" "narrow.css")
        )
      )
      (?
        (c.getLengthSx (hu.extraCssPath))
        (hb.tagA "link"
          ('
            (hb.attribute "rel" "stylesheet")
            (hb.attribute "type" "text/css")
            (hb.attribute "href" (hu.extraCssPath))
          )
        )
        (c.empty)
      )
      (?
        (c.getLengthSx (hu.extraNarrowCssPath))
        (hb.tagA "link"
          ('
            (hb.attribute "rel" "stylesheet")
            (hb.attribute "type" "text/css")
            (hb.attribute "media" "(max-width:800px)")
            (hb.attribute "href" (hu.extraNarrowCssPath))
          )
        )
        (c.empty)
      )
      (hb.tagAE "script" (hb.attribute "src" "search.js") "")
      (hb.tagAE "script" (hb.attribute "src" "https://www.googletagmanager.com/gtag/js?id=UA-133906591-2") "")
      (hb.tagE "script" "window.dataLayer = window.dataLayer || []; function gtag(){dataLayer.push(arguments);} gtag('js', new Date()); gtag('config', 'UA-133906591-2')")
    )
  )
}

[Html Body]

{hb.content; ;
  (hb.tagCE "body" "hBody"
    ('
      (hb.tagCE "header" "hHeader"
        (hb.image "hBanner" (hp.getRelativePath "images/br.jpg"))
      )
      (hb.tagCE "div" "hBeneathHeader"
        ('
          (hb.tagCE "div" "hMenuSide"
            (hb.tagCE "div" "hMenuBack"
              ('
                (hb.tagCE "div" "hMenuButtonBack"
                  (hb.tagACE "button" "hMenuButton" (hb.attribute "onclick" "menu()")
                    (hb.image "hMenuButtonImage" (hp.getRelativePath "images/menu.png"))
                  )
                )
                (hb.tagACE "div" "hMenuLink"
                  ('
                    (hb.attribute "style" "display: none; pointer-events: none;")
                    (hb.attribute "id" "menuLink")
                  )
                  (hm.contentOfMenuLink)
                )
                (hb.tagACE "div" "hMenuWord"
                  ('
                    (hb.attribute "style" "display: none; pointer-events: none;")
                    (hb.attribute "id" "menuWord")
                  )
                  (c.empty)
                )
              )
            )
          )
          (hb.tagCE "div" "hContentSide"
            (hb.tagCE "div" "hContentBack" (hc.content))
          )
          (hb.tagCE "div" "hFooterSide"
            (hb.tagCE "footer" "hFooter"
              ('
                (hb.tagACE "button" "hFooterButton" (hb.attribute "onclick" "scrollToTop")
                  (hb.image "hFooterButtonImage" (hp.getRelativePath "images/top.png"))
                )
                (hb.tagCE "span" "hFooterText" "(C) 2020 駒場東邦物理部")
              )
            )
          )
        )
      )
    )
  )
}

{hb.attribute Name Value; ; (' " " Name "=\"" Value "\"")}
{hb.tag Tag; ; (' "<" Tag ">\r\n")}
{hb.tagA Tag Attribute; ; (' "<" Tag Attribute ">\r\n")}
{hb.tagAE Tag Attribute Content; ; (' "<" Tag Attribute ">\r\n" Content "</" Tag ">\r\n")}
{hb.tagE Tag Content; ; (hb.tagAE Tag "" Content)}
{hb.tagAC Tag Class Attribute; ; (hb.tagA Tag (' (hb.attribute "class" Class) Attribute))}
{hb.tagACE Tag Class Attribute Content; ; (hb.tagAE Tag (' (hb.attribute "class" Class) Attribute) Content)}
{hb.tagCE Tag Class Content; ; (hb.tagACE Tag Class "" Content)}
{hb.concatenateClass A B; ;
  ('
    A
    (? 
      (&
        (!= (c.getLengthSx A) 0)
        (!= (c.getLengthSx B) 0)
      )
      " "
      (c.empty)
    )
    B
  )
}
{hb.image Class Path; ; (hb.tagAC "image" Class (hb.attribute "src" Path))}

[Html Menu]

{hm.contentOfMenuLink; ;
  (?
    (== (hm.linkType) (hm.linkType.lectureIndex))
    (hm.lectureIndex)
    (c.empty) [WIP]
  )
}
{hm.lectureIndex; ;
  ('
    (hm.linkGroup "駒場東邦物理部"
      ('
        (hm.linkElement "トップページ" (hp.getRelativePath "index.html"))
        (hm.linkElement "講習一覧" (hp.getRelativePath "le/le.html"))
      )
    )
    (hm.lectureList)
  )
}

[Type of page for link]
{hm.linkType.top; ; 0}
{hm.linkType.lectureIndex; ; 1}
{hm.linkType.lectureElement; ; 2}

[Link Element]
{hm.linkGroup Title Array; ;
  (?
    (c.getLengthSx Array)
    (hb.tagCE "div" "hMenuLinkGroup"
      ('
        (hb.tagCE "div" "hMenuLinkTitle"
          (hb.tagCE "span" "hMenuLinkText" Title)
        )
        Array
      )
    )
    (c.empty)
  )
}
{hm.linkElement Text Path; ;
  (hb.tagACE "a" "hMenuLinkElement" (hb.attribute "href" Path)
    (hb.tagCE "span" "hMenuLinkText" Text)
  )
}
{hm.linkElementOut Text Path; ;
  (?
    (c.compareStrict Path (hp.path))
    (c.empty)
    (hm.linkElement Text Path)
  )
}

[Html Content]
{hc.heading Title; ;
  (hb.tagCE "h2" "cHeading" Title)
}
{hc.text Content; ; (hb.tagCE "span" "cText" Content)}
[^^]

{hc.articleC Class TitleClass Title Content; ;
  (hb.tagCE "article" (hb.concatenateClass "cArticle" Class)
    ('
      (hb.tagCE "h2" "cArticleTitle" Title)
      Content
    )
  )
}
{hc.article Title Content; ; (hc.articleC "" "" Title Content)}
{hc.titleC Class Title; ;
  (hb.tagCE "article" "cPageTitleBack"
    (hb.tagCE "h1" (hb.concatenateClass "cPageTitle" Class) Title)
  )
}
{hc.title Title; ; (hc.titleC "" Title)}
{hc.boxCV Class Content; ; (hb.tagCE "div" (hb.concatenateClass "cBoxV" Class) Content)}
{hc.boxV Content; ; (hc.boxCV "" Content)}
{hc.boxCH Class Content; ; (hb.tagCE "div" (hb.concatenateClass "cBoxH" Class) Content)}
{hc.boxH Content; ; (hc.boxCH "" Content)}
{hc.listC Class Content; ; (hb.tagCE "ol" (hb.concatenateClass "cList" Class) Content)}
{hc.list Content; ; (hc.listC "" Content)}
{hc.listElementC Class Content; ; (hb.tagCE "li" (hb.concatenateClass "cListElement" Class) Content)}
{hc.listElement Content; ; (hc.listElementC "" Content)}
{hc.tableC Class HeadClass Head BodyClass Body FootClass Foot; ;
  (hb.tagCE "table" (hb.concatenateClass "cTable" Class)
    ('
      (?
        (c.getLengthSx Head)
        (hb.tagCE "thead" (hb.concatenateClass "cThead" HeadClass) Head)
        (c.empty)
      )
      (hb.tagCE "tbody" (hb.concatenateClass "cTbody" BodyClass) Body)
      (?
        (c.getLengthSx Foot)
        (hb.tagCE "tfoot" (hb.concatenateClass "cFoot" FootClass) Foot)
        (c.empty)
      )
    )
  )
}
{hc.table Head Body Foot; ; (hc.tableC "" "" Head "" Body "" Foot)}
{hc.tableRowC Class Content; ; (hb.tagCE "tr" (hb.concatenateClass "cTr" Class) Content)}
{hc.tableRow Content; ; (hc.tableRowC "" Content)}
{hc.tableCellCH Class Content; ; (hb.tagCE "th" (hb.concatenateClass "cTh" Class) Content)}
{hc.tableCellH Content; ; (hc.tableCellCH "" Content)}
{hc.tableCellCB Class Content; ; (hb.tagCE "td" (hb.concatenateClass "cTd" Class) Content)}
{hc.tableCellB Content; ; (hc.tableCellCB "" Content)}
{hc.tableCellCF Class Content; ; (hb.tagCE "tf" (hb.concatenateClass "cTf" Class) Content)}
{hc.tableCellF Content; ; (hc.tableCellCF "" Content)}
{hc.tableRowCHS Class CellClass Content; ;
  (hc.tableRowC
    Class
    (hc.tableRowS.loop
      Content
      "th"
      (hb.concatenateClass "cTh" CellClass)
      (c.getLengthSx Content)
    )
  )
}
{hc.tableRowHS Content; ; (hc.tableRowCHS "" "" Content)}
{hc.tableRowCBS Class CellClass Content; ;
  (hc.tableRowC
    Class
    (hc.tableRowS.loop
      Content
      "td"
      (hb.concatenateClass "cTd" CellClass)
      (c.getLengthSx Content)
    )
  )
}
{hc.tableRowBS Content; ; (hc.tableRowCBS "" "" Content)}
{hc.tableRowCFS Class CellClass Content; ;
  (hc.tableRowC
    Class
    (hc.tableRowS.loop
      Content
      "tf"
      (hb.concatenateClass "cTf" CellClass)
      (c.getLengthSx Content)
    )
  )
}
{hc.tableRowFS Content; ; (hc.tableRowCFS "" "" Content)}
{hc.tableRowS.loop Character 16 Tag Class Count; ;
  (?
    (== Character ";")
    (hb.tagCE Tag Class (> Count 64))
    (?
      (== (: Count 64) 1)
      (hb.tagCE Tag Class (' (> Count 64) Character))
      (c.empty)
    )
  );
  ('
    (?
      (!= Character ";")
      (' (> Count 64) Character)
      (c.empty)
    )
    (- (: Count 64) 1)
  )
}
