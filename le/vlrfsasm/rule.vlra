
{.; ; (c.convertSbS8 (' "<!DOCTYPE html><html>" (hh.content) (hb.content) "</html>"))}

[Html File Path]
{hp.depth; ; (hp.depth.loop (hp.path) (' 0 (c.getLengthSx (hp.path))))}
{hp.depth.loop Path 16 Count; ;
  (?
    (== (: Count 64) 1)
    (?
      (== Path "/")
      (+ (> Count 64) 1)
      (> Count 64)
    )
    (c.empty)
  );
  ('
    (+ (> Count 64) (== Path "/"))
    (- (: Count 64) 1)
  )
}
{hp.rootPath; ; (c.repeat "../" (hp.depth))}
{hp.getRelativePath AbsolutePath; ; (' (hp.rootPath) AbsolutePath)}

[Html header]
{hh.content; ;
  ('
    "<head>"
    "<meta charset=\"UTF-8\">"
    "<meta name=\"keywords\" content=\"駒場東邦物理部,駒場東邦,駒東,KTPC,物理部," (hh.keyword) "\">"
    "<meta name=\"description\" content=\"" (hh.description) "\">"
    "<meta name=\"author\" content=\"駒場東邦物理部\">"
    "<meta http-equiv=\"content-language\" content=\"ja\">"
    "<meta name=\"viewport\" content=\"width=device-width, initial-scale=1, maximum-scale=1\">"
    "<title>" (hh.title) "</title>"
    "<link rel=\"icon\" type=\"image/jpeg\" href=\"" (hp.getRelativePath "images/logo.jpg") "\">"
    "<link rel=\"stylesheet\" type=\"text/css\" href=\"default.css\">"
    "<link rel=\"stylesheet\" type=\"text/css\" media=\"(max-width:800px)\" href=\"narrow.css\">"
    "<script src=\"search.js\"></script>"
    "<script async src=\"https://www.googletagmanager.com/gtag/js?id=UA-133906591-2\"></script>"
    "<script>window.dataLayer = window.dataLayer || []; function gtag(){dataLayer.push(arguments);} gtag('js', new Date()); gtag('config', 'UA-133906591-2');</script>"
    "</head>"
  )
}

[Html Body]

{hb.content; ;
  (hb.tagCE "body" "hBody"
    ('
      (hb.tagCE "header" "hHeader"
        (hb.image "hBanner" (hp.getRelativePath "images/br.jpg"))
      )
      (hb.tagCE "div" "hBeneathHeader"
        ('
          (hb.tagCE "div" "hMenuSide"
            (hb.tagCE "div" "hMenuBack"
              ('
                (hb.tagCE "div" "hMenuButtonBack"
                  (hb.tagACE "button" "hMenuButton" (hb.attribute "onclick" "menu()")
                    (hb.image "hMenuButtonImage" (hp.getRelativePath "images/menu.png"))
                  )
                )
                (hb.tagACE "div" "hMenuLink"
                  ('
                    (hb.attribute "style" "display: none; pointer-events: none;")
                    (hb.attribute "id" "menuLink")
                  )
                  (hm.contentOfMenuLink)
                )
                (hb.tagACE "div" "hMenuWord"
                  ('
                    (hb.attribute "style" "display: none; pointer-events: none;")
                    (hb.attribute "id" "menuWord")
                  )
                  (c.empty)
                )
              )
            )
          )
          (hb.tagCE "div" "hContentSide"
            (hb.tagCE "div" "hContentBack" (hc.content))
          )
          (hb.tagCE "div" "hFooterSide"
            (hb.tagCE "footer" "hFooter"
              ('
                (hb.tagACE "button" "hFooterButton" (hb.attribute "onclick" "scrollToTop")
                  (hb.image "hFooterButtonImage" (hp.getRelativePath "images/top.png"))
                )
                (hb.tagCE "span" "hFooterText" "(C) 2020 駒場東邦物理部")
              )
            )
          )
        )
      )
    )
  )
}

{hb.attribute Name Value; ; (' " " Name "=\"" Value "\"")}
{hb.tag Tag; ; (' "<" Tag ">\r\n")}
{hb.tagA Tag Attribute; ; (' "<" Tag Attribute ">\r\n")}
{hb.tagAE Tag Attribute Content; ; (' "<" Tag Attribute ">\r\n" Content "</" Tag ">\r\n")}
{hb.tagE Tag Content; ; (hb.tagAE Tag "" Content)}
{hb.tagAC Tag Class Attribute; ; (hb.tagA Tag (' (hb.attribute "class" Class) Attribute))}
{hb.tagACE Tag Class Attribute Content; ; (hb.tagAE Tag (' (hb.attribute "class" Class) Attribute) Content)}
{hb.tagCE Tag Class Content; ; (hb.tagACE Tag Class "" Content)}

{hb.image Class Path; ; (hb.tagAC "image" Class (hb.attribute "src" Path))}

[Html Menu]

{hm.contentOfMenuLink; ;
  (?
    (== (hm.linkType) (hm.linkType.lectureIndex))
    (hm.lectureIndex)
    (c.empty) [WIP]
  )
}
{hm.lectureIndex; ;
  ('
    (hm.linkGroup "駒場東邦物理部"
      ('
        (hm.linkElement "トップページ" (hp.getRelativePath "index.html"))
        (hm.linkElement "講習一覧" (hp.getRelativePath "le/le.html"))
      )
    )
    (hm.lectureList)
  )
}

[Type of page for link]
{hm.linkType.top; ; 0}
{hm.linkType.lectureIndex; ; 1}
{hm.linkType.lectureElement; ; 2}

[Link Element]
{hm.linkGroup Title Array; ;
  (?
    (c.getLengthSx Array)
    (hb.tagCE "div" "hMenuLinkGroup"
      ('
        (hb.tagCE "div" "hMenuLinkTitle"
          (hb.tagCE "span" "hMenuLinkText" Title)
        )
        Array
      )
    )
    (c.empty)
  )
}
{hm.linkElement Text Path; ;
  (hb.tagACE "a" "hMenuLinkElement" (hb.attribute "href" Path)
    (hb.tagCE "span" "hMenuLinkText" Text)
  )
}
{hm.linkElementOut Text Path; ;
  (?
    (c.compareStrict Path (hp.path))
    (c.empty)
    (hm.linkElement Text Path)
  )
}

[Html Content]
{hc.title Title; ; (hb.tagCE "h1" "hPageTitle" Title)}
