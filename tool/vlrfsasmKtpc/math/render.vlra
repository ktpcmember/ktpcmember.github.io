
{mr.render Content Base Scale; ;
  (?
    (# Content)
    (?
      (<= (mc.getOpcode Content) 7)
      (?
        (<= (mc.getOpcode Content) 3)
        (?
          (<= (mc.getOpcode Content) 1)
          (?
            (== (mc.getOpcode Content) 0)
            (mr.string Content Base Scale)
            (mr.text Content Base Scale)
          )
          (?
            (== (mc.getOpcode Content) 2)
            (mr.concatenate
              (mc.decode Content)
              Base
              Scale
            )
            (mr.attachment
              (mc.decode Content)
              Base
              Scale
            )
          )
        )
        (?
          (<= (mc.getOpcode Content) 5)
          (?
            (== (mc.getOpcode Content) 4)
            (mr.overline Content Base Scale)
            (mr.underline Content Base Scale)
          )
          (?
            (== (mc.getOpcode Content) 6)
            (mr.sqrt Content Base Scale)
            (mr.root Content Base Scale)
          )
        )
      )
      (?
        (== (mc.getOpcode Content) 8)
        (mr.fraction Content Base Scale)
        (mr.smallFraction Content Base Scale)
      )
    )
    (c.empty)
  )
}
{mr.string Content Base Scale; ;
  (hb.tagAE "text"
    ('
      (hb.attribute "x"
        (mf.fraction2 (> Base 16))
      )
      (hb.attribute "y"
        (mf.fraction2 (: Base 16))
      )
      (hb.attribute "font-size"
        ('
          (mf.fraction2
            (ms.scale (mf.immI 16) Scale)
          )
          "px"
        )
      )
    )
    (he.escape (mc.decode Content))
  )
}
{mr.concatenate Content Base Scale; ;
  (?
    (# Content)
    ('
      (mr.render
        (mc.getFirstElement Content)
        Base
        Scale
      )
      (mr.concatenate
        (mc.getRestElementList Content)
        ('
          (+
            (> Base 16)
            (ms.scale
              (mc.getSizeX
                (mc.getFirstElement Content)
              )
              Scale
            )
          )
          (: Base 16)
        )
        Scale
      )
    )
    (c.empty)
  )
}
{mr.overline Content Base Scale; ;
  ('
    (hb.xmlTagA "path"
      ('
        (hb.attribute "d"
          ('
            "M "
            (mf.fraction2 (> Base 16)) " "
            (mf.fraction2
              (-
                (: Base 16)
                (ms.scale
                  (+
                    (mc.getSizeH
                      (mc.decode Content)
                    )
                    (ms.basic 1 0)
                  )
                  Scale
                )
              )
            ) " h "
            (mf.fraction2
              (ms.scale
                (mc.getSizeX Content)
                Scale
              )
            )
          )
        )
        (hb.attribute "stroke" "black")
      )
    )
    (mr.render
      (mc.decode Content)
      Base
      Scale
    )
  )
}
{mr.underline Content Base Scale; ;
  ('
    (hb.xmlTagA "path"
      ('
        (hb.attribute "d"
          ('
            "M "
            (mf.fraction2 (> Base 16)) " "
            (mf.fraction2
              (+
                (: Base 16)
                (ms.scale
                  (mc.getSizeL
                    (mc.decode Content)
                  )
                  Scale
                )
                (ms.basic 1 Scale)
              )
            ) " h "
            (mf.fraction2
              (ms.scale
                (mc.getSizeX Content)
                Scale
              )
            )
          )
        )
        (hb.attribute "stroke" "black")
      )
    )
    (mr.render
      (mc.decode Content)
      Base
      Scale
    )
  )
}
{mr.sqrt Content Base Scale; ;
  ('
    (hb.xmlTagA "path"
      ('
        (hb.attribute "d"
          (mr.sqrt.path
            (> Base 16)
            (: Base 16)
            (ms.scale
              (mc.getSizeX (mc.decode Content))
              Scale
            )
            (ms.scale
              (mc.getSizeL (mc.decode Content))
              Scale
            )
            (ms.scale
              (mc.getSizeY (mc.decode Content))
              Scale
            )
            (mf.immI 0)
            Scale
          )
        )
        (hb.attribute "stroke" "black")
        (hb.attribute "fill" "none")
      )
    )
    (mr.render
      (mc.decode Content)
      ('
        (+
          (> Base 16)
          (ms.basic 5 Scale)
        )
        (: Base 16)
      )
      Scale
    )
  )
}
{mr.sqrt.path
  BaseX BaseY SizeX SizeL SizeY ExponentWidth Scale; ;
  ('
    "M " (mf.fraction2 BaseX) " "
    (mf.fraction2
      (+
        BaseY
        SizeL
        (mf.mul SizeY (mf.immF $FFD5)) [-0.336]
      )
    ) " h "
    (mf.fraction2
      (mz.max-
        (ms.basic 1 Scale)
        (-
          (ms.scale ExponentWidth (+ Scale 1))
          (ms.basic 2 Scale)
        )
      )
    ) " l "
    (mf.fraction2 (ms.basic 1 Scale)) " "
    (mf.fraction2 (mf.mul SizeY (mf.immF $002B)))
    " l "
    (mf.fraction2 (ms.basic 3 Scale)) " "
    (mf.fraction2
      (c.negate (+ SizeY (ms.basic 1 Scale)))
    ) " h "
    (mf.fraction2 SizeX)
  )
}
{mr.root Content Base Scale; ;
  ('
    (hb.xmlTagA "path"
      ('
        (hb.attribute "d"
          (mr.sqrt.path
            (> Base 16)
            (: Base 16)
            (ms.scale
              (mc.getSizeX (mc.decode Content))
              Scale
            )
            (ms.scale
              (mc.getSizeL (mc.decode Content))
              Scale
            )
            (ms.scale
              (mc.getSizeY (mc.decode Content))
              Scale
            )
            (mc.getSizeX
              (mc.getRestChildList Content)
            )
            Scale
          )
        )
        (hb.attribute "stroke" "black")
        (hb.attribute "fill" "none")
      )
    )
    ('
      (mr.render
        (mc.getFirstChild Content)
        ('
          (+
            (> Base 16)
            (ms.basic 2 Scale)
            (c.max
              (ms.basic 3 Scale)
              (ms.scale
                (mc.getSizeX
                  (mc.getRestChildList Content)
                )
                (+ Scale 1)
              )
            )
          )
          (: Base 16)
        )
        Scale
      )
      (mr.render
        (mc.getRestChildList Content)
        ('
          (+
            (> Base 16)
            (mz.max-
              (mf.immI 0)
              (-
                (ms.basic 3 Scale)
                (ms.scale
                  (mc.getSizeX
                    (mc.getRestChildList Content)
                  )
                  (+ Scale 1)
                )
              )
            )
          )
          (-
            (: Base 16)
            (ms.scale
              (+
                (mf.mul
                  (mc.getSizeH Base)
                  (mf.immF $002B)
                )
                (mf.mul
                  (mc.getSizeL Base)
                  (mf.immF $FFAB)
                )
                (ms.basic 1 0)
                (ms.scale
                  (mc.getSizeL
                    (mc.getRestChildList Content)
                  )
                  1
                )
              )
              Scale
            )
          )
        )
        (+ Scale 1)
      )
    )
  )
}
{mr.fraction Content Base Scale; ;
  ('
    (hb.xmlTagA "path"
      ('
        (hb.attribute "d"
          ('
            "M " (mf.fraction2 (> Base 16)) " "
            (mf.fraction2
              (- (: Base 16) (ms.basic 4 0))
            ) " h "
            (mf.fraction2 (mc.getSizeX Content))
          )
        )
        (hb.attribute "stroke" "black")
      )
    )
    (mr.render
      (mc.getFirstChild Content)
      ('
        (+
          (> Base 16)
          (mf.mul
            (ms.scale
              (-
                (mc.getSizeX Content)
                (mc.getSizeX
                  (mc.getFirstChild Content)
                )
              )
              Scale
            )
            (mf.immF $40)
          )
        )
        (-
          (: Base 16)
          (+
            (ms.scale
              (mc.getSizeL
                (mc.getFirstChild Content)
              )
              Scale
            )
            (ms.basic 5 Scale)
          )
        )
      )
      Scale
    )
    (mr.render
      (mc.getRestChildList Content)
      ('
        (+
          (> Base 16)
          (mf.mul
            (ms.scale
              (-
                (mc.getSizeX Content)
                (mc.getSizeX
                  (mc.getRestChildList Content)
                )
              )
              Scale
            )
            (mf.immF $40)
          )
        )
        (+
          (: Base 16)
          (-
            (ms.scale
              (mc.getSizeH
                (mc.getRestChildList Content)
              )
              Scale
            )
            (ms.basic 3 Scale)
          )
        )
      )
      Scale
    )
  )
}
{mr.attachment Content Base Scale; ;
  ('
    (mr.render
      (mc.getFirstElement Content)
      ('
        (+
          (> Base 16)
          (ms.scale
            (c.getRange Content 112 16)
            Scale
          )
        )
        (: Base 16)
      )
      Scale
    )
    (mr.attachment.recursion
      (mc.getRestElementList (> Content 128))
      (+
        (> Base 16)
        (ms.scale
          (c.getRange Content 112 16)
          Scale
        )
      )
      (: Base 16) Scale (: Content 16)
      (ms.scale (c.getRange Content 96 16) Scale)
      (ms.scale (c.getRange Content 80 16) Scale)
      (ms.scale (c.getRange Content 64 16) Scale)
    )
  )
}
{mr.attachment.recursion Content BaseX BaseY Scale Position CenterX CenterH CenterL; ;
  (?
    Position
    ('
      (mr.attachment.recursion
        (mc.getRestElementList Content)
        BaseX BaseY Scale (> Position 4)
        CenterX CenterH CenterL
      )
      (mr.render
        (mc.getFirstElement Content)
        ('
          (?
            (&
              (>= (: Position 4) 4)
              (<= (: Position 4) 7)
            )
            (-
              BaseX
              (mr.attachment.scaleX Content Scale)
            )
            (?
              (|
                (== (: Position 4) 3)
                (== (: Position 4) 8)
              )
              (+
                BaseX
                (mf.mul
                  (-
                    CenterX
                    (mr.attachment.scaleX
                      Content
                      Scale
                    )
                  )
                  (mf.immF $40)
                )
              )
              (+ BaseX CenterX)
            )
          )
          (?
            (<= (: Position 4) 5)
            (?
              (== (: Position 4) 3)
              (-
                BaseY
                (+
                  CenterH
                  (mr.attachment.scaleL
                    Content
                    Scale
                  )
                  (ms.basic 1 0)
                )
              )
              (?
                (|
                  (== (: Position 4) 2)
                  (== (: Position 4) 4)
                )
                (-
                  BaseY
                  (mz.max-
                    CenterH
                    (+
                      (mf.mul
                        (- CenterH CenterL)
                        (mf.immF $40)
                      )
                      (mr.attachment.scaleL
                        Content
                        Scale
                      )
                      (ms.basic 1 Scale)
                    )
                  )
                )
                (-
                  BaseY
                  (+
                    (mf.mul
                      (- CenterH CenterL)
                      (mf.immF $40)
                    )
                    (mr.attachment.scaleL
                      Content
                      Scale
                    )
                    (ms.basic 1 Scale)
                  )
                )
              )
            )
            (?
              (== (: Position 4) 8)
              (+
                BaseY
                CenterL
                (mr.attachment.scaleH
                  Content
                  Scale
                )
                (ms.basic 1 0)
              )
              (?
                (|
                  (== (: Position 4) 7)
                  (== (: Position 4) 9)
                )
                (+
                  (mr.attachment.scaleH
                    Content
                    Scale
                  )
                  BaseY
                )
                (+
                  (mf.mul
                    (- CenterL CenterH)
                    (mf.immF $40)
                  )
                  (mr.attachment.scaleH
                    Content
                    Scale
                  )
                  (ms.basic 1 Scale)
                  BaseY
                )
              )
            )
          )
        )
        (+ Scale 1)
      )
    )
    (c.empty)
  )
}
{mr.attachment.scaleX List Scale; ;
  (ms.scale
    (mc.getSizeX (mc.getFirstElement List))
    (+ Scale 1)
  )
}
{mr.attachment.scaleH List Scale; ;
  (ms.scale
    (mc.getSizeH (mc.getFirstElement List))
    (+ Scale 1)
  )
}
{mr.attachment.scaleL List Scale; ;
  (ms.scale
    (mc.getSizeL (mc.getFirstElement List))
    (+ Scale 1)
  )
}
{mr.attachment.scaleY List Scale; ;
  (ms.scale
    (mc.getSizeY (mc.getFirstElement List))
    (+ Scale 1)
  )
}
