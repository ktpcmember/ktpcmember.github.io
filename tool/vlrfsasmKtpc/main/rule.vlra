
{.; ; (c.convertSbS8 (' "<!DOCTYPE html><html>" (hh.content) (hb.content) "</html>"))}

[Html File Path]
{hp.depth; ; (hp.depth.loop (hp.path) (' 0 (c.getLengthSx (hp.path))))}
{hp.depth.loop Path 16 Count; ;
  (?
    (== (: Count 64) 1)
    (?
      (== Path "/")
      (+ (> Count 64) 1)
      (> Count 64)
    )
    (c.empty)
  );
  ('
    (+ (> Count 64) (== Path "/"))
    (- (: Count 64) 1)
  )
}
{hp.rootPath; ; (c.repeat "../" (hp.depth))}
{hp.getRelativePath AbsolutePath; ; (' (hp.rootPath) AbsolutePath)}

[Html header]
{hh.content; ;
  (hb.tagE "head"
    ('
      (hb.tagA "meta" (hb.attribute "charset" "UTF-8"))
      (hb.tagA "meta"
        ('
          (hb.attribute "name" "keywords")
          (hb.attribute "content" (' "駒場東邦物理部,駒場東邦,駒東,KTPC,物理部," (hh.keyword)))
        )
      )
      (hb.tagA "meta"
        ('
          (hb.attribute "name" "description")
          (hb.attribute "content" (hh.description))
        )
      )
      (hb.tagA "meta"
        ('
          (hb.attribute "name" "author")
          (hb.attribute "content" "駒場東邦物理部")
        )
      )
      (hb.tagA "meta"
        ('
          (hb.attribute "http-equiv" "content-language")
          (hb.attribute "content" "ja")
        )
      )
      (hb.tagA "meta"
        ('
          (hb.attribute "name" "viewport")
          (hb.attribute "content" "width=device-width, initial-scale=1, maximum-scale=1")
        )
      )
      (hb.tagE "title" (hh.title))
      (hb.tagA "link"
        ('
          (hb.attribute "rel" "icon")
          (hb.attribute "type" "image/jpeg")
          (hb.attribute "href" (hp.getRelativePath "images/logo.jpg"))
        )
      )
      (hb.tagA "link"
        ('
          (hb.attribute "rel" "stylesheet")
          (hb.attribute "type" "text/css")
          (hb.attribute "href" (hp.getRelativePath "tool/vlrfsasmKtpc/style/default.css"))
        )
      )
      (hb.tagA "link"
        ('
          (hb.attribute "rel" "stylesheet")
          (hb.attribute "type" "text/css")
          (hb.attribute "media" "(max-width:800px)")
          (hb.attribute "href" (hp.getRelativePath "tool/vlrfsasmKtpc/style/narrow.css"))
        )
      )
      (?
        (c.getLengthSx (hu.extraCssPath))
        (hb.tagA "link"
          ('
            (hb.attribute "rel" "stylesheet")
            (hb.attribute "type" "text/css")
            (hb.attribute "href" (hu.extraCssPath))
          )
        )
        (c.empty)
      )
      (?
        (c.getLengthSx (hu.extraNarrowCssPath))
        (hb.tagA "link"
          ('
            (hb.attribute "rel" "stylesheet")
            (hb.attribute "type" "text/css")
            (hb.attribute "media" "(max-width:800px)")
            (hb.attribute "href" (hu.extraNarrowCssPath))
          )
        )
        (c.empty)
      )
      (hb.tagAE "script" (hb.attribute "src" (hp.getRelativePath "tool/vlrfsasmKtpc/js/search.js")) "")
      (hb.tagAE "script" (hb.attribute "src" "https://www.googletagmanager.com/gtag/js?id=UA-133906591-2") "")
      (hb.tagE "script" "window.dataLayer = window.dataLayer || []; function gtag(){dataLayer.push(arguments);} gtag('js', new Date()); gtag('config', 'UA-133906591-2')")
    )
  )
}

[Html Body]

{hb.content; ;
  (hb.tagCE "body" "hBody"
    ('
      (hb.tagCE "header" "hHeader"
        (hb.imageC "hBanner" (hp.getRelativePath "images/br.jpg"))
      )
      (hb.tagCE "div" "hBeneathHeader"
        ('
          (hb.tagCE "div" "hMenuSide"
            (hb.tagCE "div" "hMenuBack"
              ('
                (hb.tagCE "div" "hMenuButtonBack"
                  (hb.tagACE "button" "hMenuButton" (hb.attribute "onclick" "menu()")
                    (hb.imageC "hMenuButtonImage" (hp.getRelativePath "images/menu.png"))
                  )
                )
                (hb.tagACE "div" "hMenuLink"
                  ('
                    (hb.attribute "style" "display: none; pointer-events: none;")
                    (hb.attribute "id" "menuLink")
                  )
                  (hm.contentOfMenuLink)
                )
                (hb.tagACE "div" "hMenuWord"
                  ('
                    (hb.attribute "style" "display: none; pointer-events: none;")
                    (hb.attribute "id" "menuWord")
                  )
                  (c.empty)
                )
              )
            )
          )
          (hb.tagCE "div" "hContentSide"
            (hb.tagCE "div" "hContentBack" (hc.content))
          )
          (hb.tagCE "div" "hFooterSide"
            (hb.tagCE "footer" "hFooter"
              ('
                (hb.tagACE "button" "hFooterButton" (hb.attribute "onclick" "scrollToTop()")
                  (hb.imageC "hFooterButtonImage" (hp.getRelativePath "images/top.png"))
                )
                (hb.tagCE "span" "hFooterText" "(C) 2020 駒場東邦物理部")
              )
            )
          )
        )
      )
    )
  )
}

{hb.attribute Name Value; ;
  (?
    (# Value)
    (' " " Name "=\"" Value "\"")
    (c.empty)
  )
}
{hb.tag Tag; ; (' "<" Tag ">")}
{hb.tagA Tag Attribute; ; (' "<" Tag Attribute ">")}
{hb.tagAE Tag Attribute Content; ; (' "<" Tag Attribute ">" Content "</" Tag ">")}
{hb.tagE Tag Content; ; (hb.tagAE Tag "" Content)}
{hb.tagAC Tag Class Attribute; ; (hb.tagA Tag (' (hb.attribute "class" Class) Attribute))}
{hb.tagACE Tag Class Attribute Content; ; (hb.tagAE Tag (' (hb.attribute "class" Class) Attribute) Content)}
{hb.tagCE Tag Class Content; ; (hb.tagACE Tag Class "" Content)}
{hb.concatenateClass A B; ;
  ('
    A
    (? 
      (&
        (!= (c.getLengthSx A) 0)
        (!= (c.getLengthSx B) 0)
      )
      " "
      (c.empty)
    )
    B
  )
}
{hb.imageC Class Path; ; (hb.tagAC "image" Class (hb.attribute "src" Path))}

[Xml Tag]
{hb.xmlTag Tag; ; (' "<" Tag "/>")}
{hb.xmlTagA Tag Attribute; ; (' "<" Tag Attribute "/>")}

[Html Menu]

{hm.contentOfMenuLink; ;
  (?
    (== (hm.linkType) (hm.linkType.lectureIndex))
    (hm.lectureIndex)
    (?
      (== (hm.linkType) (hm.linkType.lectureSection))
      (hm.lectureSection)
      (c.empty) [WIP]
    )
  )
}
{hm.lectureIndex; ;
  ('
    (hm.linkGroup "駒場東邦物理部"
      ('
        (hm.linkElement "トップページ" (hp.getRelativePath "index.html"))
        (hm.linkElement "講習一覧" (hp.getRelativePath "le/le.html"))
      )
    )
    (hm.lectureList)
  )
}
{hm.lectureSection; ;
  ('
    (hm.linkGroup "駒場東邦物理部"
      ('
        (hm.linkElement "トップページ" (hp.getRelativePath "index.html"))
        (hm.linkElement "講習トップ" (hp.getRelativePath "le/le.html"))
      )
    )
    (hm.linkGroup
      (hm.lectureName)
      ('
        (?
          (# (hm.previousSectionPath))
          (hm.linkElement
            (hm.previousSectionName)
            (hm.previousSectionPath)
          )
          (c.empty)
        )
        (hm.linkElement
          "講習トップ"
          (hm.lectureTopPath)
        )
        (?
          (# (hm.nextSectionPath))
          (hm.linkElement
            (hm.nextSectionName)
            (hm.nextSectionPath)
          )
          (c.empty)
        )
      )
    )
  )
}

[Type of page for link]
{hm.linkType.top; ; 0}
{hm.linkType.lectureIndex; ; 1}
{hm.linkType.lectureSection; ; 2}

[Link Element]
{hm.linkGroup Title Array; ;
  (?
    (c.getLengthSx Array)
    (hb.tagCE "div" "hMenuLinkGroup"
      ('
        (hb.tagCE "div" "hMenuLinkTitle"
          (hb.tagCE "span" "hMenuLinkText" Title)
        )
        Array
      )
    )
    (c.empty)
  )
}
{hm.linkElement Text Path; ;
  (hb.tagACE "a" "hMenuLinkElement" (hb.attribute "href" Path)
    (hb.tagCE "span" "hMenuLinkText" Text)
  )
}
{hm.linkElementOut Text Path; ;
  (?
    (c.compareStrict Path (hp.path))
    (c.empty)
    (hm.linkElement Text Path)
  )
}

[Html Content]
{hc.heading Title; ;
  (hb.tagCE "h2" "cHeading" Title)
}
{hc.text Content; ; (hb.tagCE "span" "cText" Content)}
[^^]

{hc.articleC Class Title Content; ;
  (hb.tagCE "article" (hb.concatenateClass "cArticle" Class)
    ('
      (hb.tagCE "h2" "cArticleTitle" Title)
      Content
    )
  )
}
{hc.article Title Content; ; (hc.articleC "" Title Content)}
{hc.articleCCa Class Title Caption Content; ;
  (hc.articleC
    Class
    Title
    (' (hb.tagCE "div" "cArticleCaption" Caption) Content)
  )
}
{hc.articleCa Title Caption Content; ;
  (hc.articleCCa "" Title Caption Content)
}
{hc.sectionC Class Title Content; ;
  (hb.tagCE "section" (hb.concatenateClass "cSection" Class)
    ('
      (hb.tagCE "h2" "cSectionTitle" Title)
      Content
    )
  )
}
{hc.section Title Content; ; (hc.sectionC "" Title Content)}
{hc.titleC Class Title; ;
  (hb.tagCE "article" "cPageTitleBack"
    (hb.tagCE "h1" (hb.concatenateClass "cPageTitle" Class) Title)
  )
}
{hc.title Title; ; (hc.titleC "" Title)}
{hc.p Content; ; (hb.tagCE "p" "cP" Content)}
{hc.boxCV Class Content; ; (hb.tagCE "div" (hb.concatenateClass "cBoxV" Class) Content)}
{hc.boxV Content; ; (hc.boxCV "" Content)}
{hc.boxCH Class Content; ; (hb.tagCE "div" (hb.concatenateClass "cBoxH" Class) Content)}
{hc.boxH Content; ; (hc.boxCH "" Content)}
{hc.listC Class Content; ; (hb.tagCE "ol" (hb.concatenateClass "cList" Class) Content)}
{hc.list Content; ; (hc.listC "" Content)}
{hc.listElementC Class Content; ; (hb.tagCE "li" (hb.concatenateClass "cListElement" Class) Content)}
{hc.listElement Content; ; (hc.listElementC "" Content)}
{hc.table Caption Class Content; ;
  (hb.tagCE "table" Class
    (' (hb.tagE "caption" Caption) Content)
  )
}
{hc.tr Content; ; (hb.tagE "tr" Content)}
{hc.td Content Style; ;
  (hb.tagAE "td"
    (hc.td.loop
      ('
        Style
        (?
          (&
            (!= (# Style) 0)
            (!= (: Style 16) ",")
          )
          ","
          (c.empty)
        )
      )
      ""
    )
    Content
  )
}
{hc.td.loop Style 16 Condition; ;
  (?
    (== Style ",")
    (?
      (== (c.getRangeSx Condition 0 1) "#")
      (hb.attribute "style"
        (' "background-color: " Condition ";")
      )
      (hb.attribute "class" Condition)
    )
    (c.empty)
  );
  (? (== Style ",") (c.empty) (' Condition Style))
}

[Alias]
{ha.replace A; ; (ha.select (ha.find (aliasList.txt) A))}

[extension]
{hx.l A B; ; (hb.tagACE "a" "cLink" (hb.attribute "href" B) A)}
{hx.a A; ; (ha.replace A)}
{hx.S A B; ; (hb.tagCE "span" A B)}
{hx.b A; ; (hx.S "xBold" A)}
{hx.i A; ; (hx.S "xItalic" A)}
{hx.o A; ; (hx.S "xOverline" A)}
{hx.u A; ; (hx.S "xUnderline" A)}
{hx.uRed A; ; (hx.S "xUnderlineRed" A)}
{hx.uBlue A; ; (hx.S "xUnderlineBlue" A)}
{hx.uGreen A; ; (hx.S "xUnderlineGreen" A)}

{hx.c A; ; (qh.single "c" A)}
{hx.cMT A B; ; (qh.multi "c" A B 1)}
{hx.P A; ; (qh.single "pa" A)}
{hx.PMT A B; ; (qh.multi "pa" A B 1)}
{hx.v A; ; (qh.single "v" A)}
{hx.vMT A B; ; (qh.multi "v" A B 1)}
{hx.qBatMT A B; ; (hx.PMT A B)} [For the time being]
{hx.T A; ; (hx.P A)}
{hx.TMT A B; ; (hx.PMT A B)} [For the time being]

{hx.m A; ; (mh.math A)}
{hx.mo A; ; (mh.overline A)}
{hx.mu A; ; (mh.underline A)}
{hx.s A; ; (mh.sqrt A)}
{hx.mr A B; ; (mh.root A B)}
{hx.f A B; ; (mh.fraction A B)}
{hx.mf A B; ; (mh.smallFraction A B)}
{hx.0 A B; ; (mh.attachment1 "0" A B)}
{hx.1 A B; ; (mh.attachment1 "1" A B)}
{hx.2 A B; ; (mh.attachment1 "2" A B)}
{hx.3 A B; ; (mh.attachment1 "3" A B)}
{hx.4 A B; ; (mh.attachment1 "4" A B)}
{hx.5 A B; ; (mh.attachment1 "5" A B)}
{hx.6 A B; ; (mh.attachment1 "6" A B)}
{hx.7 A B; ; (mh.attachment1 "7" A B)}
{hx.8 A B; ; (mh.attachment1 "8" A B)}
{hx.9 A B; ; (mh.attachment1 "9" A B)}
{hx.Z A B C D; ; (mh.attachment2 B A C D)}
{hx.D A B C D E; ; (mh.attachment3 B A C D E)}
{hx.V A B C D E F; ; (mh.attachment4 B A C D E F)}

